spriteset(spriteset_koki_50000_purchase, "gfx/freight/koki_50000.png") {
	template_purchase(1, 352)
}

//base
spriteset(spriteset_koki_50000_empty_a, "gfx/freight/koki_50000.png") {
	template_2cc_std(1, 1)
}

spriteset(spriteset_koki_50000_empty_b, "gfx/freight/koki_50000.png") {
	template_2cc_std(1, 32)
}

//handrail

spriteset(spriteset_koki_50000_handrail, "gfx/freight/koki_50000.png") {
	template_2cc_std(1, 320)
}

//brake van

spriteset(spriteset_koki_50000_brakevan_front, "gfx/freight/koki_50000.png") {
	template_container_1(1, 64)
}

spriteset(spriteset_koki_50000_brakevan_rear, "gfx/freight/koki_50000.png") {
	template_container_4(1, 64)
}

random_switch (FEAT_TRAINS, SELF, random_koki_50000_brown) {
	2: spriteset_koki_50000_empty_a;
	1: spriteset_koki_50000_empty_b;
}


//brakevan test


switch(FEAT_TRAINS, SELF, switch_kokifu_50000_up_loadstack, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 7 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0:  random_koki_50000_brown;
    1:  switch_conts_cargo_selection;
	2:	switch_conts_cargo_selection;
	3:	switch_conts_cargo_selection;
	4:	spriteset_koki_50000_brakevan_rear;
	5:  spriteset_blank;
	6:  switch_taillights_8;
}

switch(FEAT_TRAINS, SELF, switch_kokifu_50000_down_loadstack, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 7 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0:  random_koki_50000_brown;
    1:  spriteset_koki_50000_brakevan_rear;
	2:	switch_conts_cargo_selection;
	3:	switch_conts_cargo_selection;
	4:	switch_conts_cargo_selection;
	5:  spriteset_blank;
	6:  switch_taillights_8;
}

switch(FEAT_TRAINS, SELF, switch_kokifu_50000_direction, direction) {
	DIRECTION_SOUTHEAST: switch_kokifu_50000_down_loadstack;
	DIRECTION_SOUTH: switch_kokifu_50000_down_loadstack;
	DIRECTION_SOUTHWEST: switch_kokifu_50000_down_loadstack;
	switch_kokifu_50000_up_loadstack;
}






switch(FEAT_TRAINS, SELF, switch_koki_50000_base_loadstack, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 7 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0:  random_koki_50000_brown;
    1:  switch_conts_cargo_selection;
	2:  switch_conts_cargo_selection;
	3:  switch_conts_cargo_selection;
	4:  switch_conts_cargo_selection;
	5:  spriteset_koki_50000_handrail;
	6:  switch_taillights_8;
}

switch(FEAT_TRAINS, SELF, switch_koki_50000_caboose, position_in_consist_from_end) {
	0: switch_kokifu_50000_direction;
	switch_koki_50000_base_loadstack;
	}

item (FEAT_TRAINS, koki_50000, 1414) {
	property {
		sprite_id: SPRITE_ID_NEW_TRAIN;
		track_type: NAAN;
		name: string(STR_NAME_KOKI_50000);
		climates_available: ALL_CLIMATES;
		introduction_date: date(1971,1,1);
		speed: 100 km/h;
		air_drag_coefficient: 0;
		power: 0;
		dual_headed: 0;
		cargo_capacity: 70; 
		refit_cost: 0;
		default_cargo_type: GOOD;
		loading_speed: 35;
		refittable_cargo_classes: bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_LIQUID); 
		non_refittable_cargo_classes: 0; 
		cargo_allow_refit: [MAIL, GOOD, OIL_, PAPR, FOOD, RUBR, FRUT, WATR, SUGR, TOYS, BATT, STEL, METL, SWET, TOFF, COLA, CTCD, BUBL, PLST, FZDR, BEER, COPR, FICR, BDMT, CERA, CMNT, DYES, ENSP, FERT, FISH, FMSP, FRVG, GLAS, MILK, MNSP, PETR, PLAS, RCYC, RFPR, SULP, WDPR, WOOL, VEHI, EOIL, ACID, CHLO]; 
		cargo_disallow_refit: [PASS, COAL, LVST, GRAI, WOOD, IORE, VALU, WHEA, GOLD, MAIZ, CORE, DIAM, AORE, CERE, CLAY, GRVL, LIME, OLSD, POTA, SAND, SCMT, SGBT, SGCN, TOUR, JAVA];
		weight: 20;
		cost_factor: 9;
		running_cost_factor: 11;
		length: 8;
		misc_flags: bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_SPRITE_STACK);
	}
	graphics {
		cargo_capacity: return 70;
		default: switch_koki_50000_caboose;
		purchase: spriteset_koki_50000_purchase;
		refit_cost: CB_RESULT_AUTOREFIT;
	}
}