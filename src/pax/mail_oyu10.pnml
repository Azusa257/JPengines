spriteset(spriteset_oyu_10_brown, "gfx/paxmail/oyu10.png") {
	template_2cc_std(1, 1)
}

spriteset(spriteset_oyu_10_blue, "gfx/paxmail/oyu10.png") {
	template_2cc_std(1, 32)
}

spriteset(spriteset_oyu_10_blue_ac, "gfx/paxmail/oyu10.png") {
	template_2cc_std(1, 64)
}

spriteset(spriteset_oyu_10_brown_purchase, "gfx/paxmail/oyu10.png") {
	template_purchase(1, 160)
}

spriteset(spriteset_oyu_10_blue_purchase, "gfx/paxmail/oyu10.png") {
	template_purchase(1, 192)
}

random_switch(FEAT_TRAINS, SELF, random_oyu_10_blue_ac) {
	1: spriteset_oyu_10_blue;
	1: spriteset_oyu_10_blue_ac;
	}

switch(FEAT_TRAINS, SELF, switch_oyu_10_yeardep_livery, build_year) {
	0..1968: spriteset_oyu_10_brown;
	1968..1975: spriteset_oyu_10_blue;
	random_oyu_10_blue_ac;
}


switch(FEAT_TRAINS, SELF, switch_oyu_10_livery, cargo_subtype) {
	1: spriteset_oyu_10_brown;
	2: spriteset_oyu_10_blue;
    3: spriteset_oyu_10_blue_ac;
	switch_oyu_10_yeardep_livery;
}


switch(FEAT_TRAINS, SELF, switch_oyu_10_subtype_text, cargo_subtype) {
	0: return string(STR_REFIT_YEARDEP);
	1: return string(STR_REFIT_JNR_BROWN);
	2: return string(STR_REFIT_BLUE);
    3: return string(STR_REFIT_BLUE_AC);
	   return CB_RESULT_NO_TEXT;
	}

switch(FEAT_TRAINS, SELF, switch_oyu_10_purchase_yeardep, current_year) {
    0..1968: spriteset_oyu_10_brown_purchase;
    spriteset_oyu_10_blue_purchase;
    }



item (FEAT_TRAINS, mail_oyu_10, 1668) {
	property {
		sprite_id: SPRITE_ID_NEW_TRAIN;
		track_type: NAAN;
		name: string(STR_NAME_OYU_10);
		climates_available: ALL_CLIMATES;
		introduction_date: date(1956,1,1);
		speed: 100 km/h;
		air_drag_coefficient: 0;
		power: 0;
		dual_headed: 0;
		cargo_capacity: 100; 
		refit_cost: 0;
		default_cargo_type: MAIL;
		loading_speed: 50;
		refittable_cargo_classes: bitmask(CC_MAIL); 
		non_refittable_cargo_classes: 0; 
		weight: 20;
		cost_factor: 11;
		running_cost_factor: 6;
		length: 8;
		misc_flags: bitmask(TRAIN_FLAG_AUTOREFIT);
	}
	graphics {
//		cargo_capacity: return 75;
		default: switch_oyu_10_livery;
		purchase: switch_oyu_10_purchase_yeardep;
		cargo_subtype_text: switch_oyu_10_subtype_text;
		
	}
}